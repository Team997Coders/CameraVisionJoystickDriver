plugins {
  id 'java'
  id 'application'
}

mainClassName = "Main"
project.ext.set('nativeLibsDir', "$buildDir/libs/natives")

repositories {
  mavenCentral()
}

configurations {
  nativeBundle
}

dependencies {
  // https://mvnrepository.com/artifact/net.java.jinput/jinput
  implementation 'net.java.jinput:jinput:2.0.9'
  implementation 'com.beust:jcommander:1.72'
  if (System.getProperty("os.name").toLowerCase().contains("windows")) {
    nativeBundle 'net.java.jinput:windows-plugin:2.0.9:natives-windows'
  } else if (System.getProperty("os.name").toLowerCase().contains("os x")) {
    nativeBundle 'net.java.jinput:osx-plugin:2.0.9:natives-osx'
  } else {
    nativeBundle 'net.java.jinput:osx-plugin:2.0.9:natives-linux'
  }
}

task extractNativeBundle(type: Sync) {
  from {
    configurations.nativeBundle.collect { zipTree(it) }
  }
  into file(project.nativeLibsDir)
}

assembleDist.dependsOn extractNativeBundle

run {
  dependsOn extractNativeBundle
  systemProperty "java.library.path", project.nativeLibsDir
}